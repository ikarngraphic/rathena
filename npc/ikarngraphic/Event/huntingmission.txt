//===== rAthena Script =======================================
//= Hunting Missions
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: =========================================
//= Random hunting missions.
//= Rewards are based on quest difficulty.
//= 
//= NOTE: Requires SQL mob database.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Small improvements and fixes.
//= 1.2 Added party support and replaced blacklists with an
//=     SQL query, both thanks to AnnieRuru.
//= 1.3 Re-added a blacklist adapted for the SQL query.
//= 1.3a Added mission reset options.
//= 1.3b Function updates.
//= 1.4 Check for deleted characters, thanks to AnnieRuru.
//=     Syntax updates and style cleaning.
//============================================================

-	shop	mission_shop	-1,512:-1

1@herbs,113,59,4	script	Hunting Missions	951,{
function Chk;
	mes "[Hunting Missions]";
	mes "หวัดดี, " + strcharinfo(0) + "!";
	if (!#Mission_Delay) {
		next;
		mes "[Hunting Missions]";
		mes "ข้าไม่เคยเห็นเจ้ามาก่อน...";
		mes "เจ้าต้องเพิ่งมาแน่ๆ!";
		emotion ET_HUK;
		next;
		callsub Mission_Info;
		emotion ET_GO;
		#Mission_Delay = 1;
		close;
	}
	mes rand(2) ? "ทำงานหนัก, เหมือนเดิม..." : "ข้าหวังว่า, จะไม่ได้พัก...";
	mes "มีอะไรที่ข้าพอจะช่วย";
	mes "เจ้าได้บ้าง ?";
	mes " ";
	mes "^777777~ เจ้าสำเร็จ " + callfunc("F_InsertPlural",Mission_Total,"ภารกิจ",0,"^0055FF%d^777777 %s") + ". ~^000000";
	next;
	switch(select(
		((!Mission0) ? " ~ ภารกิจใหม่::" : ": ~ สถานะภารกิจ: ~ ละทิ้งภารกิจ") +
		": ~ รายละเอียด: ~ ร้านค้าภารกิจ: ~ ส่องอันดับนักล่า: ~ ^777777ยกเลิก^000000"
	)) {
	case 1:
		mes "[Hunting Missions]";
		if (#Mission_Count) {
			mes "เจ้าได้เริ่มภารกิจ";
			mes "โดยตัวละครอื่น.";
			if (!@hm_char_del_check) {  // check for deleted character
				query_sql("SELECT 1 FROM `char_reg_num` WHERE `key` = 'Mission0' AND `char_id` IN(SELECT `char_id` FROM `char` WHERE `account_id` = " + getcharid(3) + ")", .@i);
				if (!.@i) {
					next;
					mes "[Hunting Missions]";
					mes "ดูเหมือนว่าข้าจะไม่สามารถ";
					mes "หาตัวละครนั้นพบ, ได้...";
					mes "กรุณา, รอแป๊ปหนึ่ง.";
					emotion ET_SCRATCH;
					#Mission_Count = 0;
				}
				@hm_char_del_check = 1;
			}
			close;
		}
		if (#Mission_Delay > gettimetick(2) && .Delay) {
			mes "ข้าเกรงว่าเจ้าจะต้องรอ " + callfunc("Time2Str",#Mission_Delay) + " ก่อนถึงรับภารกิจต่อได้.";
			close;
		}
		mes "เจ้าต้องล่า:";
		query_sql("SELECT ID FROM `" + .mob_db$ + "` WHERE left(Sprite, 4) != 'meta' AND left(Sprite, 2) != 'E_' AND ~Mode & 32 AND EXP > 0 AND MVP1id = 0 AND DropCardid > 4000 AND DropCardid < 5000 AND ID < 2000 AND instr('"+.Blacklist$+"',ID) = 0 ORDER BY rand() LIMIT " + .Quests, .@mob);
		for (.@i = 0; .@i < .Quests; .@i++) {
			setd "Mission" + .@i, .@mob[.@i];
			setd "Mission" + .@i +"_", 0;
		}
		#Mission_Count = rand(.Count[0], .Count[1]);
		callsub Mission_Status;
		next;
		mes "[Hunting Missions]";
		mes "กลับมารายงานเมื่อ";
		mes "เจ้าสำเร็จภารกิจ.";
		mes "ขอให้โชคดี!";
		close;
	case 2:
		mes "[Hunting Missions]";
		mes "สถานะภารกิจ:";
		callsub Mission_Status;
		close;
	case 3:
		mes "[Hunting Missions]";
		mes "เจ้าต้องการที่จะ";
		mes "ละทิ้งภารกิจใช่ไหม ?";
		if (.Reset < 0 && .Delay)
			mes "เวลาดีเลย์ภารกิจจะไม่ถูกรีไปด้วย.";
		else if (.Reset > 0)
			mes "มันมีค่าใช้จ่าย " + callfunc("F_InsertComma",.Reset) + " Zeny.";
		next;
		switch(select(" ~ ละทิ้งภารกิจ...: ~ ^777777ยกเลิก^000000")) {
		case 1:
			if (.Reset > 0) {
				if (Zeny < .Reset) {
					mes "[Hunting Missions]";
					mes "เจ้าไม่มีเงินพอ";
					mes "ที่จะละทิ้งภารกิจ.";
					emotion ET_SORRY;
					close;
				}
				Zeny -= .Reset;
				emotion ET_MONEY;
			}
			mes "[Hunting Missions]";
			mes "เข้าใจแล้ว, ข้าจะทำการ";
			mes "ละทิ้งภารกิจให้เดี๋ยวนี้้.";
			specialeffect2 EF_STORMKICK4;
			for (.@i = 0; .@i < .Quests; .@i++) {
				setd "Mission"+.@i, 0;
				setd "Mission"+.@i+"_", 0;
			}
			#Mission_Count = 0;
			if (.Reset < 0 && .Delay)
				#Mission_Delay = gettimetick(2) + (.Delay * 3600);
			close;
		case 2:
			mes "[Hunting Missions]";
			mes "ข้ารู้อยู่แล้วว่าเจ้าไม่ทำหรอก!";
			mes "ตั้งใจทำงานเข้า.";
			emotion ET_SMILE;
			close;
		}
	case 4:
		callsub Mission_Info;
		close;
	case 5:
		mes "[Hunting Missions]";
		mes "เจ้ามี ^0055FF" + #Mission_Points + "^000000 แต้มภารกิจ.";
		mes "ใช้อย่างมีสติ!";
		callshop "mission_shop",1;
		npcshopattach "mission_shop";
		end;
	case 6:
		mes "[Hunting Missions]";
		mes "อันดับนักล่า:";
		query_sql("SELECT char_id AS id, (SELECT `name` FROM `char` WHERE char_id = id),`value` FROM `char_reg_num` WHERE `key` = 'Mission_Total' ORDER BY CAST(`value` AS SIGNED) DESC LIMIT 5", .@id, .@name$, .@val);
		for (.@i = 0; .@i < 5; .@i++)
			mes "  [อันดับ " + (.@i+1) + "]  " + ((.@name$[.@i] == "") ? "^777777ไม่มี" : "^0055FF" + .@name$[.@i]+"^000000 : ^FF0000" + .@val[.@i] + " pt.") + "^000000";
		close;
	case 7:
		mes "[Hunting Missions]";
		mes "ไม่มีอะไร ? Okay...";
		emotion ET_SCRATCH;
		close;
	}
	end;

Mission_Status:
	@f = 0;
	deletearray .@j[0], getarraysize(.@j);
	for (.@i = 0; .@i < .Quests; .@i++) {
		.@j[.@i] = getd("Mission" + .@i);
		.@j[.Quests] = .@j[.Quests] + strmobinfo(3,.@j[.@i]);
		.@j[.Quests+1] = .@j[.Quests+1] + (strmobinfo(6,.@j[.@i]) / (getbattleflag("base_exp_rate") / 100) * .Modifier[0]);
		.@j[.Quests+2] = .@j[.Quests+2] + (strmobinfo(7,.@j[.@i]) / (getbattleflag("job_exp_rate") / 100) * .Modifier[1]);
		mes " > "+Chk(getd("Mission"+.@i+"_"),#Mission_Count) + strmobinfo(1,.@j[.@i]) + " (" + getd("Mission"+.@i+"_") + "/" + #Mission_Count + ")^000000";
	}

	// Reward formulas:
	.@Mission_Points = 3 + (.@j[.Quests] / .Quests / 6);
	//.@Base_Exp = #Mission_Count * .@j[.Quests+1] / 5;
	//.@Job_Exp = #Mission_Count * .@j[.Quests+2] / 5;
	//.@Zeny = #Mission_Count * .Quests * .@j[.@i] * .Modifier[2];

	next;
	mes "[Hunting Missions]";
	mes "รางวัลภารกิจ:";
	mes " > แต้มภารกิจ: ^0055FF" + .@Mission_Points + "^000000";
	//mes " > ค่าประสบการณ์: ^0055FF" + callfunc("F_InsertComma",.@Base_Exp) + "^000000";
	//mes " > ค่าประสบการณ์อาชีพ: ^0055FF" + callfunc("F_InsertComma",.@Job_Exp) + "^000000";
	//mes " > เงิน: ^0055FF" + callfunc("F_InsertComma",.@Zeny) + "^000000";
	if (@f) {
		@f = 0;
		return;
	}
	next;
	mes "[Hunting Missions]";
	mes "โอ้, เจ้าทำสำเร็จ!";
	mes "เยี่ยมมาก.";
	mes "รับรางวัลของเจ้าไป.";
	emotion ET_BEST;
	specialeffect2 EF_ANGEL;
	specialeffect2 EF_TRUESIGHT;
	#Mission_Points += .@Mission_Points;
	BaseExp += .@Base_Exp;
	JobExp += .@Job_Exp;
	Zeny += .@Zeny;
	for (.@i = 0; .@i < .Quests; .@i++) {
		setd "Mission" + .@i, 0;
		setd "Mission" + .@i+"_", 0;
	}
	#Mission_Count = 0;
	if (.Delay)
		#Mission_Delay = gettimetick(2) + (.Delay * 3600);
	Mission_Total++;
	if (Mission_Total == 1)
		query_sql("INSERT INTO `char_reg_num` (`char_id`,`key`,`index`,`value`) VALUES (" + getcharid(0) + ",'Mission_Total','0',1)");
	else
		query_sql("UPDATE `char_reg_num` SET `value` = " + Mission_Total + " WHERE `char_id` = " + getcharid(0) + " AND `key` = 'Mission_Total'");
	close;

Mission_Info:
	mes "[Hunting Missions]";
	mes "เมื่อเจ้าเลือกที่จะทำ, ข้าจะเริ่ม";
	mes "สุ่มภารกิจให้เจ้า.";
	mes "บางทีภารกิจอาจง่าย, แต่";
	mes "ยิ่งยาก รางวัลยิ่งดีตาม.";
	next;
	mes "[Hunting Missions]";
	mes "แต้มภารกิจสามารถใช้ร่วมกับ";
	mes "ตัวละครอื่นๆของเจ้าได้.";
	if (.Delay)
		mes "เวลาดีเลย์, ก็เหมือนกัน.";
	mes "เจ้าไม่สามารถรับภารกิจ";
	mes "พร้อมๆกันกับตัวละครอื่นๆได้.";
	next;
	mes "[Hunting Missions]";
	mes "เจ้าสามารถรับภารกิจได้";
	mes (.Delay ? "ทุก " + ((.Delay == 1) ? "ชั่วโมง." : .Delay + " ชั่วโมง.") : "เมื่อเจ้าต้องการ.");
	mes "ได้เสมอ~";
	return;

function Chk {
	if (getarg(0) < getarg(1)) {
		@f = 1;
		return "^FF0000";
	} else
		return "^00FF00";
}

OnBuyItem:
	@cost = 0;
	for (.@i = 0; .@i < getarraysize(@bought_nameid); .@i++)
		for (.@j = 0; .@j < getarraysize(.Shop); .@j += 2)
			if (@bought_nameid[.@i] == .Shop[.@j]) {
				set @cost, @cost + (.Shop[.@j+1] * @bought_quantity[.@i]);
				break;
			}
	mes "[Hunting Missions]";
	if (@cost > #Mission_Points)
		mes "เจ้ามีแต้มภารกิจไม่พอ.";
	else {
		for (.@i = 0; .@i < getarraysize(@bought_nameid); .@i++) {
			getitem @bought_nameid[.@i], @bought_quantity[.@i];
			dispbottom "เลือกซื้อ " + @bought_quantity[.@i] + "x " + getitemname(@bought_nameid[.@i]) + ".";
		}
		#Mission_Points -= @cost;
		mes "การซื้อสมบูรณ์.";
		emotion ET_MONEY;
	}
	@cost = 0;
	deletearray @bought_nameid[0], getarraysize(@bought_nameid);
	deletearray @bought_quantity[0], getarraysize(@bought_quantity);
	close;

OnNPCKillEvent:
	if (!getcharid(1) || !.Party) {
		if (!#Mission_Count || !Mission0) end;
		for (.@i = 0; .@i < .Quests; .@i++) {
			if (strmobinfo(1,killedrid) == strmobinfo(1,getd("Mission" + .@i))) {
				if (getd("Mission" + .@i + "_") < #Mission_Count) {
					dispbottom "[Hunting Mission] ได้ล่า " + (set(getd("Mission" + .@i + "_"),getd("Mission" + .@i + "_") + 1)) +
					           " ของ " + #Mission_Count + " " + strmobinfo(1,killedrid) + ".";
					end;
				}
			}
		}
	} else if (.Party) {
		.@mob = killedrid;
		getmapxy(.@map1$,.@x1,.@y1,UNITTYPE_PC);
		getpartymember getcharid(1),1;
		getpartymember getcharid(1),2;
		for (.@i = 0; .@i < $@partymembercount; .@i++) {
			if (isloggedin($@partymemberaid[.@i], $@partymembercid[.@i])) {
				set .@Mission_Count, getvar(#Mission_Count, $@partymembercid[.@i]);
				set .@Mission0, getvar(Mission0, $@partymembercid[.@i]);
				set .@HP, readparam(HP, $@partymembercid[.@i]);

				if (.@Mission_Count && .@Mission0 && .@HP > 0) {
					getmapxy(.@map2$,.@x2,.@y2,UNITTYPE_PC);
					if ((.@map1$ == .@map2$ || .Party == 1) && (distance(.@x1,.@y1,.@x2,.@y2) <= 30 || .Party < 3)) {
						for (.@j = 0; .@j < .Quests; .@j++) {
							.@my_mob_id = getvar( getd("Mission"+.@j),$@partymembercid[.@i] );
							.@my_count = getvar( getd("Mission"+.@j+"_"), $@partymembercid[.@i] );
							if (strmobinfo(1,.@mob) == strmobinfo(1,.@my_mob_id)) {
								if (.@my_count < .@Mission_Count) {
									setd "Mission"+.@j+"_", (.@my_count+1), $@partymembercid[.@i];
									dispbottom "[Hunting Mission] ได้ล่า " + (.@my_count+1) + " ของ " + .@Mission_Count + " " + strmobinfo(1,.@mob) + ".", 0x777777, $@partymembercid[.@i];
									break;
								}
							}
						}
					}
				}
			}
		}
	}
	end;

OnInit:
	.Delay = 0;            // ดีเลย์การรับหรือยกเลิกเควส, ในกี่ชั่วโมง (0 คือยกเลิก).
	.Quests = 1;            // จำนวนของ subquests ต่อภารกิจ (เพิ่มรางวัล).
	.Party = 0;             // Party options: 0 (ไม่รวมมอนที่ปาร์ตี้ฆ่า), 1 (รวมมอนที่ปาร์ตี้ฆ่า), 2 (ในแผนที่เดียวกันเท่านั้น), 3 (ในระยะหน้าจอเท่านั้น)
	.Reset = 0;            // Reset options: -1 (ละทิ้งภารกิจตั้งค่าที่ delay time), 0 (ไม่มีดีเลย์), [Zeny] (จ่ายเป็นค่าละทิ้งภารกิจ, หรือยกเลิกดีเลย์)
	setarray .Count[0],     // Min and max จำนวนมอนต่อภารกิจย่อย (เพิ่มรางวัล).
		40,60;
	setarray .Modifier[0],  // Multipliers for Base Exp, Job Exp, and Zeny rewards.
		getbattleflag("base_exp_rate")/100,getbattleflag("job_exp_rate")/100,60;
	.mob_db$ =              // Table name of SQL mob database
		(checkre(0))?"mob_db_re":"mob_db";
	setarray .Shop[0],      // ร้านแลกของ: <ID>,<point cost> (ประมาณ 10~20 points ต่อภารกิจ).
		2733,250,2720,250,2721,250,2722,250,2723,250,2724,250,2725,250,2394,840,2549,630,2444,580,2772,1200,1486,2000,1426,2000,1187,2000,13418,2000,13417,2000,13416,2000,13042,2000,13307,2000,1382,2000,1310,2000,1546,2000,1641,2000,1640,2000,2002,2000,1981,2000,1927,2000,1282,2000,1281,2000,1743,2000,1827,2000,1826,2000,1577,2000,1576,2000,13179,2000,13178,2000,13177,2000,13176,2000,13110,2000;
	.Blacklist$ =           // Blacklisted mob IDs.
		"1062,1088,1183,1186,1200,1212,1220,1221,1234,1235,"+
		"1244,1245,1250,1268,1290,1293,1294,1296,1298,1299,"+
		"1300,1301,1303,1304,1305,1306,1308,1309,1311,1313,"+
		"1515,1588,1618,1676,1677,1678,1679,1796,1797,1974,"+
		"1975,1976,1977,1978,1979";

	npcshopdelitem "mission_shop",512;
	for (.@i = 0; .@i < getarraysize(.Shop); .@i += 2)
		npcshopadditem "mission_shop", .Shop[.@i], .Shop[.@i+1];
	end;
}